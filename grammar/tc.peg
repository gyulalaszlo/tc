start
  = package:PackageDecl contents:Contents !. { return ["COMPILATION_UNIT", package, contents ]; }

NL_CHARS = [\n\r]

COMMENT "Comment"
  = "//" (!NL_CHARS . )*


WS_PART
  = COMMENT
  / [\t\v\f\n\r \u00A0\uFEFF]+

WS "whitespace"
  = WS_PART*

SE "Statement Terminator ';'"
  = [;\n\r] WS?

////////////////////////////////////////////////////////////////////////////////

PackageDecl "Package declaration"
  = "package" WS name:Identifier SE { return [ "PACKAGE", name  ] };


Contents "Compilation Unit contents"
  = statements:StatementList { return [ "CONTENTS", statements ]; }


StatementList "Statement List"
  = Statement*

Statement "Statement"
  = TypeStatement
  / MethodList

////////////////////////////////////////////////////////////////////////////////

TypeStatement "Type Statement"
  = name:TypeStatementDeclaration "=" WS definition:TypeDefinition_ SE { return ["TYPEDECL", name, definition]; }


TypeStatementDeclaration "Type Declaration"
  = "type" WS name: Identifier WS { return name; }


TypeDefinition_ "Type Definition"
  = s:StructDefinition_ { return s; }
  / a:AliasDefinition_ { return a; }
  / d:ClassDefinition_ { return d; }

////////////////////////////////////////////////////////////////////////////////

ClassDefinition_
  = "class" WS fields:StructFieldList_ { return ["CLASS", fields]; }


////////////////////////////////////////////////////////////////////////////////
StructDefinition_
  = "struct" WS fields:StructFieldList_ { return ["STRUCT", fields]; }


StructFieldList_
  = "{" WS fields:FieldDeclaration* "}" { return ["FIELDS", fields]; }


FieldDeclaration "Struct Field Declaration"
  = decl:VariableDeclaration_ SE { return decl; }

VariableDeclaration_ "Variable Declaration"
  = type:TypeName_ WS name:Identifier { return ["VAR", type, name ]; }

////////////////////////////////////////////////////////////////////////////////

AliasDefinition_ "Alias"
  = "alias" WS name:TypeName_ { return ["ALIAS", name];  }

TypeName_ "Type Name"
  = name:Identifier { return ["TYPE", name];  }

////////////////////////////////////////////////////////////////////////////////

Identifier "identifier"
  = name:IdentifierName { return ["IDENTIFIER", name];  }

IdentifierName
  = start:[a-zA-Z_] cont:[a-zA-Z0-9_]* { return [start, cont.join("")].join(""); }

////////////////////////////////////////////////////////////////////////////////

MethodList "Method list"
  = access:MethodAccessor WS name:Identifier
    WS "=" WS body:MethodListBody SE
    { return ["METHODS", name, access, body ]; }

MethodListBody "Method List body"
  = "{" WS  decls:MethodDeclaration* "}" { return decls; }

MethodDeclaration "Method Declaration"
  = name:Identifier WS "=" WS func:FunctionDeclaration
    { return ["METHOD", name, func]; }

MethodAccessor "Method acess controll"
  = "public"
  / "protected"
  / "private"


FunctionDeclaration "Function declaration"
  = "fn" WS args:FunctionArgList ret:FunctionReturnType body:FunctionBody
    { return ["FUNC", args, body, ret]; }

FunctionArgList "Type list for function parameters/return values"
  = "(" WS args:FunctionDeclarationArgumentList ")" WS { return args; }

FunctionReturnType
  = list: TypeList { return list; }

FunctionDeclarationArgumentList
  = first:FunctionArgumentFirst WS more:FunctionArgumentMore*
    { return [ "ARGS", [first].concat( more ) ];}
  / { return [] }

FunctionArgumentFirst
  = var_decl:VariableDeclaration_ { return ["ARG", var_decl]; }


FunctionArgumentMore
  = ',' WS var_decl:VariableDeclaration_ WS { return ["ARG", var_decl]; }


FunctionBody
  = "{" WS  "}" WS { return []; }


TypeList "A list of types"
  = first:Typename_ WS more:MoreTypes* { return ["TYPELIST", [first].concat(more)]; }
  / { return ["TYPELIST", []]; }

MoreTypes = "," WS more:Typename_ WS

Typename_ "Type name"
  = name:Identifier { return name; }
